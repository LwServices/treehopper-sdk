plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion '25.0.3'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:21.0.0'
    testCompile 'junit:junit:4.12'
    compile project(':api:treehopper')
}

group = 'io.treehopper.android'
version = '1.11.1'

publishing {
    publications {
        Production(MavenPublication) {
            groupId 'io.treehopper.android'
            artifactId 'treehopper.android'
            version '1.11.1'
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'Treehopper'
        name = 'treehopper.android'
        userOrg = 'treehopper-electronics'
        licenses = ['MIT']
        websiteUrl = "https://treehopper.io"
        publish = true
        vcsUrl = 'https://github.com/treehopper-electronics/treehopper-sdk.git'
        version {
            name = '1.11.1'
            desc = 'Treehopper connectivity for desktop Java apps'
            released  = new Date()
            vcsTag = '1.11.1'
        }
    }

    publications = ['Production']

}